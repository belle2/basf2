Import('env')

import os
from SCons.Script import *
from SCons.Scanner.C import CScanner

# the main directory where nsm2 is located
src_dir = '.'

def build_library(name, dirs, alias_name, libs = []):
    """build library"""
    # collect source files and install header files
    source_files = []
    for dir in dirs:
        includes = env.Install(os.path.join(env['INCDIR'], 'nsm2'), Glob(os.path.join(src_dir, dir, '*.h')))
        env.Alias(alias_name, includes)
        source_files.append([os.path.join(env['BUILDDIR'], "daq/slc/nsm2", str(source_file))
                             for source_file in Glob(os.path.join(src_dir, dir, '*.c'))])
    lib = env.SharedLibrary(os.path.join(env['LIBDIR'], name), [source_files], LIBS=libs)
    env.Alias(alias_name, lib)

# We build a single library out of the nsm2 copied to the basf2 repository.
# To not interfere with any other nsm library on any system, we give it a basf2 related name
build_library("libnsm2_basf2.so", ['corelib', 'b2lib'], "nsm2_basf2")

# stop scons from further exploring the nsm2 folder
env['CONTINUE'] = False

Return('env')
