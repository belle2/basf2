
CDECK  ID>, AMPLIM. 

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C    THIS FUNCTION CALCULATES THE AMPLITUDE FOR THE
C    GAMMA IN T-CHANNEL FOR THE PROCESS
C    E-(P1) + E+(P2) ----> E-(P3) + E+(P4) + G(P5) + G(P6)
C    WITH BREMSS. FROM THE SAME                  E+ OR E-  WITH IND=1
C    WITH BREMSS. FROM DIFFERENT                 E+ OR E-  WITH IND=2
C    WITH BREMSS. FROM BOTH INITIAL OR FINAL     E+ &  E-  WITH IND=3
C    WITH BREMSS. FROM ONE INITIAL AND ONE FINAL E+ &  E-  WITH IND=4
C
C                            M.MARTINEZ & R.MIQUEL   BARCELONA-87
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      FUNCTION AMPLIM(P1,L1,P2,L2,P3,L3,P4,L4,P5,L5,P6,L6,IND)
      IMPLICIT DOUBLE PRECISION(A-H,M,O-Z)
      COMPLEX*16 ZMART,SP,SM
      COMPLEX*16 AMPLIM,ZREP1,ZREP5
      INTEGER P,P1,P2,P3,P4,P5,P6
      COMMON / PRODUX / SP,SM,U,E,D
      COMMON /MCONST / PI,DR,SR2,PB
      COMMON /MLEPT1/ME,ME2
C
C  ME IS THE ELECTRON MASS
C  ME2 = ME**2
      DIMENSION SP(6,6),SM(6,6),D(6,6),E(6),U(6),B(6)
C
C  INITIALIZATION
      DATA B/1.D0, 1.D0,-1.D0,-1.D0,-1.D0,-1.D0/
      DATA P/3/
C
      AMPLIM = (0.D0,0.D0)
C
C  NORMALIZATION FACTORS FOR THE PHOTON POLARIZATION VECTOR
      ZN5=1.D0/(SR2 * CDABS(SP(P,P5)) )
      ZN6=1.D0/(SR2 * CDABS(SP(P,P6)) )
C
C   LOOP OVER REPEATED INDEX "IL" AND  "ILP"
      DO 100 I1=1,2
      ILP   = 2*I1 - 3
      ZREP5 = ZMART(P5,ILP,P1,L1,P,L5,P5,L5)
      ZREP1 = ZMART(P1,ILP,P1,L1,P,L5,P5,L5)
      DO 100 I2=1,2
      IL    = 2*I2 - 3
      IF     (IND.EQ.1) THEN
      AMPLIM = AMPLIM +
     .   ZMART(P2,L2,P4,L4,P3,L3,P1,IL)             * B(P1) *
     .     (ZMART(P1,IL,P5,ILP,P,L6,P6,L6)  * ZREP5 * B(P5)
     .     +ZMART(P1,IL,P1,ILP,P,L6,P6,L6)  * ZREP1 * B(P1))
     . + ZMART(P2,L2,P4,L4,P3,L3,P5,IL)             * B(P5) *
     .     (ZMART(P5,IL,P5,ILP,P,L6,P6,L6)  * ZREP5 * B(P5)
     .     +ZMART(P5,IL,P1,ILP,P,L6,P6,L6)  * ZREP1 * B(P1))
     . + ZMART(P2,L2,P4,L4,P3,L3,P6,IL)             * B(P6) *
     .     (ZMART(P6,IL,P5,ILP,P,L6,P6,L6)  * ZREP5 * B(P5)
     .     +ZMART(P6,IL,P1,ILP,P,L6,P6,L6)  * ZREP1 * B(P1))
C
      ELSE IF(IND.EQ.2) THEN
      AMPLIM  = AMPLIM +
     .   ZMART(P3,L3,P6,IL,P,L6,P6,L6)              * B(P6) *
     .     (ZMART(P2,L2,P4,L4,P6,IL,P1,ILP) * ZREP1 * B(P1)
     .    + ZMART(P2,L2,P4,L4,P6,IL,P5,ILP) * ZREP5 * B(P5))
     . + ZMART(P3,L3,P3,IL,P,L6,P6,L6)              * B(P3) *
     .     (ZMART(P2,L2,P4,L4,P3,IL,P1,ILP) * ZREP1 * B(P1)
     .    + ZMART(P2,L2,P4,L4,P3,IL,P5,ILP) * ZREP5 * B(P5))
C
      ELSE IF(IND.EQ.3) THEN
      AMPLIM  = AMPLIM +
     .   ZMART(P2,L2,P6,IL,P,L6,P6,L6)              * B(P6) *
     .     (ZMART(P3,L3,P1,ILP,P6,IL,P4,L4) * ZREP1 * B(P1)
     .    + ZMART(P3,L3,P5,ILP,P6,IL,P4,L4) * ZREP5 * B(P5))
     . + ZMART(P2,L2,P2,IL,P,L6,P6,L6)              * B(P2) *
     .     (ZMART(P3,L3,P1,ILP,P2,IL,P4,L4) * ZREP1 * B(P1)
     .    + ZMART(P3,L3,P5,ILP,P2,IL,P4,L4) * ZREP5 * B(P5))
C
      ELSE IF(IND.EQ.4) THEN
      AMPLIM  = AMPLIM +
     .   ZMART(P6,IL,P4,L4,P,L6,P6,L6)              * B(P6) *
     .     (ZMART(P3,L3,P1,ILP,P2,L2,P6,IL) * ZREP1 * B(P1)
     .    + ZMART(P3,L3,P5,ILP,P2,L2,P6,IL) * ZREP5 * B(P5))
     . + ZMART(P4,IL,P4,L4,P,L6,P6,L6)              * B(P4) *
     .     (ZMART(P3,L3,P1,ILP,P2,L2,P4,IL) * ZREP1 * B(P1)
     .    + ZMART(P3,L3,P5,ILP,P2,L2,P4,IL) * ZREP5 * B(P5))
      ENDIF
 100  CONTINUE
C
C   PROPAGATORS
      PROP1 = B(P2)*B(P4)*D(P2,P4) + 2.D0*ME2
      PROP2 = B(P1)*B(P5)*D(P1,P5)
      PROP3 = B(P1)*B(P5)*D(P1,P5) +
     .        B(P1)*B(P6)*D(P1,P6) +
     .        B(P6)*B(P5)*D(P6,P5)
      PROP4 = B(P3)*B(P6)*D(P3,P6)
      PROP5 = B(P1)*B(P5)*D(P1,P5) +
     .        B(P1)*B(P3)*D(P1,P3) +
     .        B(P3)*B(P5)*D(P3,P5) + 2.D0*ME2
      PROP6 = B(P2)*B(P6)*D(P2,P6)
      PROP7 = B(P4)*B(P6)*D(P4,P6)
C
      IF (IND.EQ.1) AMPLIM = AMPLIM*ZN5*ZN6/PROP1/PROP2/PROP3
      IF (IND.EQ.2) AMPLIM = AMPLIM*ZN5*ZN6/PROP1/PROP2/PROP4*(-1.D0)
      IF (IND.EQ.3) AMPLIM = AMPLIM*ZN5*ZN6/PROP2/PROP5/PROP6*(-1.D0)
      IF (IND.EQ.4) AMPLIM = AMPLIM*ZN5*ZN6/PROP2/PROP5/PROP7
C
      RETURN
      END
